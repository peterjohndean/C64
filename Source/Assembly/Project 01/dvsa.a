;
; Check if anything is in the storage area,
; if so dump what it finds.
;
!zone DVSA {
DVSA:
	; Is BASIC variable storage empty?
	+CMP16 MM_VARTAB, MM_ARYTAB
	bne .PVSA
	rts
	
.PVSA:
	; Initialise
    lda #1
    sta Index
    
    lda #0
    sta Bool
    
    ;
    ; Header
    +BASIC_STROUT .HDR1
	
	;
    ; (Size, Count)
    +KERNEL_CHROUT '('
    
    ;
    ; Calculate bytes used
    +SUB16 MM_ARYTAB, MM_VARTAB, Ptr3	; Ptr3 =  MM_ARYTAB - MM_VARTAB
    +BASIC_LINPRT Ptr3					; Output Ptr3 value
    +KERNEL_CHROUT ','
    
    ;
    ; Calculate variables held
    +BASIC_GIVAYF Ptr3			; Convert Ptr3 to FAC1
    jsr BASIC_MOVAF				; Copy FAC1 to FAC2
    +BASIC_GIVAYF_IMM $0007		; FAC1 = 7
    jsr BASIC_DIV				; FAC1 = FAC2 / FAC1
    jsr BASIC_FOUT				; Convert FAC1 to String
    jsr BASIC_STROUT			; Output FAC1 String.
    
    ;
    ; Location [xxxx-yyyy]
    +BASIC_STROUT .HDR2
    +OUTPUT_HEXWORD MM_VARTAB			; Address Start
    +KERNEL_CHROUT '-'
    +SUB16_IMM MM_ARYTAB, $0001, Ptr3	; Adjust End Address: Ptr3 =  MM_ARYTAB - 1
    +OUTPUT_HEXWORD Ptr3				; Address End
    +KERNEL_CHROUT ']'
    jsr BASIC_GOCR						; Newline
	
	;
    ; Dump Table
    lda MM_VARTAB
    sta Ptr1
    lda MM_VARTAB + 1
    sta Ptr1 + 1
    
    lda MM_ARYTAB
    sta Ptr2
    lda MM_ARYTAB + 1
    sta Ptr2 + 1
    jsr DumpTable
    
    rts
;
!address .HDR1  !pet "basic variable storage", 13, 0
!address .HDR2	!pet ") [", 0
}