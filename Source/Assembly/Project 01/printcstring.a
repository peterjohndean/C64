; ------------------------------------------------------------
; Description:
;	Prints a null-terminated (C-style) string to current channel.
; 	Handles strings greater than 256 bytes too :)
; Input:
;	A (LSB)/Y (MSB) = pointer to string
; Destroys:
;	A, Y, MM_INDEX1
; ------------------------------------------------------------

!zone CUSTOM_STROUT {
!address .index = <MM_INDEX1	; Reference pointer

CustomSTROUT:
    ; A/Y = pointer to zero-terminated string
    sta .index			; lsb
    sty .index+1		; msb
    
    ldy #0				; zero offset
    
.Loop:
	lda (.index),y		; next byte from string
    beq .Done       	; [EOS] end of string, if null-terminator
    jsr KERNEL_CHROUT	; output character to channel
    iny					; increment length
    bne .Loop			; if Y didn't wrap to 0, continue
    inc .index+1		; handle page crossing - advance high byte
    jmp .Loop			; continue reading (Y is already 0)

.Done:
    rts
}